import java.nio.file.Files
import java.nio.file.Paths

/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'wiley' at '2/11/14 9:16 PM' with Gradle 1.10
 *
 */

apply plugin: 'java'
apply plugin: 'war'

sourceCompatibility = 1.7
targetCompatibility = 1.7

task wrapper(type: Wrapper) {
    gradleVersion = "2.6"
}

if (isBuildingBlock()) {
    version = getB2Version()
}

repositories {
    mavenCentral()
    maven {
        url "https://bbprepo.blackboard.com/content/repositories/releases/"
    }
}

configurations {
    b2deploy
}

dependencies {
    b2deploy 'org.oscelot:b2deploy-task:0.1.0'

    compile 'org.slf4j:slf4j-api:1.7.5'
    compile 'org.slf4j:jul-to-slf4j:1.7.5'
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'org.slf4j:log4j-over-slf4j:1.7.21'

    compile 'com.thoughtworks.xstream:xstream:1.4.7'
    compile ('net.sourceforge.stripes:stripes:1.6.0') {
        exclude module: 'log4j' // The log4j interface is implemented by org.slf4j:log4j-over-slf4j
    }
    compile 'org.springframework:spring-beans:3.2.5.RELEASE'
    compile 'org.springframework:spring-web:3.2.5.RELEASE'
    compile files('lib/bb-stripes-utils-1.1.0.jar')
    compile files('lib/b2-config-utils-1.0.3.jar')

    providedCompile 'javax.servlet:servlet-api:2.5'
    providedCompile 'jstl:jstl:1.2'
    providedCompile 'blackboard.platform:bb-platform:9.1.201404.160205'
    providedCompile 'blackboard.platform:bb-taglibs:9.1.201404.160205'
    providedCompile 'com.google.guava:guava:16.0'

    /* the bb-cms-admin is needed for the CourseEventHandler Class, as referenced in bb-manifest.xml */
    providedCompile 'blackboard.platform:bb-cms-admin:9.1.201404.160205'

    testCompile "junit:junit:4.11"
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.freemarker:freemarker:2.3.23'
    }
}

import freemarker.template.Template
import freemarker.template.TemplateExceptionHandler


ant.taskdef(name: 'b2deploy', classname: 'org.oscelot.ant.B2DeployTask', classpath: configurations.b2deploy.asPath)

task deployb2(dependsOn: 'war') << {
    println "Deploying \"" + war.archivePath + "\""
    ant.b2deploy(localfilepath: war.archivePath,
            host: 'localhost:9876',
            clean: 'true',
            courseorgavailable: 'true')
}


String getB2Version() {
    File mfFile = new File(file(webAppDir), 'WEB-INF/bb-manifest.xml');
    def manifest = new XmlSlurper().parse(mfFile);
    return manifest.plugin.version['@value'];
}

boolean isBuildingBlock() {
    File mfFile = new File(file(webAppDir), 'WEB-INF/bb-manifest.xml');
    return mfFile.exists();
}


task initB2 << {

    def console = System.console()
    if (!console) {
        logger.error("Can't get console!")
        return
    }
    def vendorId = 'atd'//System.console().readLine '\nWhat is your Vendor Id (e.g. usq, unsw, qut, swin, etc) ? '
    def vendorName = 'All The Ducks'//System.console().readLine 'What is your Vendor Name (e.g. University of Antarctica) ? '
    def vendorUrl = 'http://www.alltheducks.com'//System.console().readLine 'What is your Vendor Website (e.g. http://www.myu.edu.au/) ? '
    def b2Handle = 'blah'//System.console().readLine 'What is your B2 Handle (e.g. jshack, autosignon, etc) ? '
    def b2Name = 'Blah'//System.console().readLine 'What is your B2 Name (e.g. Student View) ? '
    def basePackage = 'com.alltheducks.blah'//System.console().readLine 'What is the base java package of your project? (e.g. au.edu.uni.myproject) ? '
//    def useStripes = System.console().readLine 'Do you want to use the Schema.xml (Y, N)?'
//    def useSpringDi = System.console().readLine 'Do you want to use Rendering Hooks (Y, N)?'
//    def useSpringDi = System.console().readLine 'Do you want to use Course Event Listeners (Y, N)?'
//    def useJersey = System.console().readLine 'Do you want to use Jersey Web Services (Y, N)?'

    println "Initialising Building Block $vendorId-$b2Handle"

    def (freemarker.template.Configuration cfg, Map<String, Object> root) = initFreemarker(vendorId, vendorName, vendorUrl, b2Handle, b2Name, basePackage);

    // commented out the following bb manifest methods to get the freemarker templating started
//    searchAndReplace("src/main/webapp/WEB-INF/bb-manifest.xml", "http://www.myu.edu/", vendorUrl)
//    searchAndReplace("src/main/webapp/WEB-INF/bb-manifest.xml", "myu", vendorId)
//    searchAndReplace("src/main/webapp/WEB-INF/bb-manifest.xml", "stub", b2Handle)
//    searchAndReplace("src/main/webapp/WEB-INF/bb-manifest.xml", "My University", vendorName)
//    searchAndReplace("src/main/webapp/WEB-INF/bb-manifest.xml", "Joe Bloggs", System.getProperty("user.name"))
//    searchAndReplace("src/main/webapp/WEB-INF/bb-manifest.xml", "helloworld", b2Handle)
//    searchAndReplace("src/main/webapp/WEB-INF/bb-manifest.xml", "myapp", "${b2Handle}-app")
    processFreemarkerFile(cfg, root, "./src/main/webapp/WEB-INF/bb-manifest.xml")

    def b2DbHandle = b2Handle.replaceAll("-", "_")
    def fullB2NameDb = vendorId + "_" + b2DbHandle

    searchAndReplace("src/main/java/edu/myinst/model/Example.java", "myu_stub", fullB2NameDb)
    searchAndReplace("src/main/webapp/WEB-INF/schema/instance/schema.xml", "myu_stub", fullB2NameDb)
    searchAndReplace("src/main/webapp/WEB-INF/schema/instance/stored_procedures.db-mssql/myu_stub_example_proc.sql", "myu_stub", fullB2NameDb)
    searchAndReplace("src/main/webapp/WEB-INF/schema/instance/stored_procedures.db-oracle/myu_stub_example_proc.sql", "myu_stub", fullB2NameDb)
    searchAndReplace("src/main/webapp/WEB-INF/schema/instance/stored_procedures.db-pgsql/myu_stub_example_proc.sql", "myu_stub", fullB2NameDb)
    searchAndReplaceFilename("src/main/webapp/WEB-INF/schema/instance/stored_procedures.db-mssql/myu_stub_example_proc.sql", "myu_stub", fullB2NameDb)
    searchAndReplaceFilename("src/main/webapp/WEB-INF/schema/instance/stored_procedures.db-oracle/myu_stub_example_proc.sql", "myu_stub", fullB2NameDb)
    searchAndReplaceFilename("src/main/webapp/WEB-INF/schema/instance/stored_procedures.db-pgsql/myu_stub_example_proc.sql", "myu_stub", fullB2NameDb)

    // Fix packages in java files
    def packagePath = basePackage.replace(".", File.separator)
    def destPackageDir = new File("src/main/java", packagePath)
    def srcPackageDir = new File("src/main/java/edu/myinst")
    destPackageDir.mkdirs();


    for (srcDir in srcPackageDir.listFiles()) {
        if (srcDir.isDirectory()) {
            def destDir = new File(destPackageDir, srcDir.getName())
            destDir.mkdir()
            for (srcFile in srcDir.listFiles()) {
                new File(destDir, srcFile.getName()).withWriter { w ->
                    srcFile.eachLine { line ->
                        w << line.replaceAll( "edu.myinst", basePackage)
                        w << "\n"
                    }
                }
            }
        }
    }

    new File("src/main/java/edu").deleteDir();

    searchAndReplace("src/main/webapp/WEB-INF/bundles/bb-manifest-en_GB.properties", "My Cool Building Block", b2Name)
    searchAndReplace("src/main/webapp/WEB-INF/bundles/bb-manifest-en_GB.properties", "Hello World", b2Name)
    searchAndReplace("src/main/webapp/WEB-INF/bundles/bb-manifest-en_GB.properties", "helloworld", b2Handle)
    searchAndReplace("src/main/webapp/WEB-INF/bundles/bb-manifest-en_GB.properties", "myapp", "${b2Handle}-app")
    searchAndReplace("src/main/webapp/WEB-INF/bundles/bb-manifest-en_GB.properties", "myu", vendorId)
    searchAndReplace("src/main/webapp/WEB-INF/bundles/bb-manifest-en_GB.properties", "stub", b2Handle)

    searchAndReplace("src/main/webapp/WEB-INF/jsp/tool.jsp", "myu", vendorId)
    searchAndReplace("src/main/webapp/WEB-INF/jsp/tool.jsp", "helloworld", b2Handle)
    searchAndReplace("src/main/webapp/WEB-INF/jsp/tool.jsp", "myapp", "${b2Handle}-app")
    searchAndReplace("src/main/webapp/WEB-INF/jsp/tool.jsp", "edu.myinst", basePackage)

    searchAndReplace("src/main/webapp/WEB-INF/jsp/config.jsp", "myu", vendorId)
    searchAndReplace("src/main/webapp/WEB-INF/jsp/config.jsp", "helloworld", b2Handle)
    searchAndReplace("src/main/webapp/WEB-INF/jsp/config.jsp", "myapp", "${b2Handle}-app")
    searchAndReplace("src/main/webapp/WEB-INF/jsp/config.jsp", "edu.myinst", basePackage)


    searchAndReplace("settings.gradle", "myu", vendorId)
    searchAndReplace("settings.gradle", "stub", b2Handle)

    searchAndReplace("src/main/webapp/WEB-INF/web.xml", "myu", vendorId)
    searchAndReplace("src/main/webapp/WEB-INF/web.xml", "stub", b2Handle)
    searchAndReplace("src/main/webapp/WEB-INF/web.xml", "edu.myinst", basePackage)


    searchAndReplace("src/main/webapp/WEB-INF/applicationContext.xml", "myu", vendorId)
    searchAndReplace("src/main/webapp/WEB-INF/applicationContext.xml", "stub", b2Handle)
    searchAndReplace("src/main/webapp/WEB-INF/applicationContext.xml", "edu.myinst", basePackage)

    def command = """git remote rm origin"""
    def proc = command.execute()
    proc.waitFor()

    println "${proc.err.text}"
    println "${proc.in.text}"
}

def initFreemarker(String vendorId, String vendorName, String vendorUrl, String b2Handle, String b2Name, String basePackage) {

    /**
     * Init a configuration object for freemarker
     * http://freemarker.org/docs/pgui_quickstart_createconfiguration.html
     */
    freemarker.template.Configuration cfg = new freemarker.template.Configuration(freemarker.template.Configuration.VERSION_2_3_23)
    cfg.setDirectoryForTemplateLoading(new File('.'))
    cfg.setDefaultEncoding('UTF-8')
    cfg.setTemplateExceptionHandler(TemplateExceptionHandler.RETHROW_HANDLER)

    // Don't log exceptions inside FreeMarker that it will thrown at you anyway
    cfg.setLogTemplateExceptions(false);

    /**
     * Create the data model
     * http://freemarker.org/docs/pgui_quickstart_createdatamodel.html
     */
    Map<String, Object> root = new HashMap<>();
    root.put("vendorId", vendorId)
    root.put("vendorName", vendorName)
    root.put("vendorUrl", vendorUrl)
    root.put("b2Handle", b2Handle)
    root.put("b2Name", b2Name)
    root.put("basePackage", basePackage)
    root.put("username", System.getProperty("user.name"))

    return [cfg, root];

}

def processFreemarkerFile(freemarker.template.Configuration cfg, Map<String, Object> root, String filepath) {

    Template template = cfg.getTemplate(filepath)
    FileWriter out = new FileWriter(new File(filepath))
    template.process(root, out)
    out.close()

}

def searchAndReplace(String targetFilePath, String search, String replace) {
    new File("${targetFilePath}.tmp").withWriter { w ->
        new File(targetFilePath).eachLine { line ->
            w << line.replaceAll( search, replace)
            w << "\n"
        }
    }
    Files.delete(Paths.get(targetFilePath))
    Files.move(Paths.get("${targetFilePath}.tmp"),
            Paths.get(targetFilePath));
}

def searchAndReplaceFilename(String targetFilePath, String search, String replace) {
    def file = new File(targetFilePath)
    def oldName = file.name
    def newName = file.name.replaceAll(search, replace)
    if(oldName != newName) {
        file.renameTo(new File(file.parentFile, newName));
    }
}
